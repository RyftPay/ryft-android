apply plugin: "maven-publish"
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For Kotlin libraries
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    if (project.plugins.findPlugin("java-library")) {
        // For Kotlin libraries
        from javadoc
    }
}

afterEvaluate { project ->
    publishing {
        publications {
            release(MavenPublication) {
                groupId rootProject.ext.group
                artifactId project.artifactId
                version rootProject.ext.version

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact sourcesJar
                if (project.plugins.findPlugin("java-library")) {
                    // For Kotlin libraries
                    artifact javadocJar
                }

                pom {
                    name = project.artifactName
                    description = project.artifactDescrption
                    url = "https://github.com/RyftPay/ryft-android"

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "https://raw.githubusercontent.com/RyftPay/ryft-android/master/LICENSE"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "ryftpay"
                            name = "Ryft"
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/RyftPay/ryft-android.git'
                        developerConnection = 'scm:git:ssh://github.com/RyftPay/ryft-android.git'
                        url = 'https://github.com/RyftPay/ryft-android/tree/master'
                    }
                }
            }
        }
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

signing {
    useInMemoryPgpKeys(
        rootProject.ext["signingKeyId"],
        rootProject.ext["signingKey"],
        rootProject.ext["signingPassword"],
    )
    sign publishing.publications
}

task cleanBuildPublishLocal(type: GradleBuild) {
    tasks = ['clean', 'build', 'publishToMavenLocal']
}