// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.7.20'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("org.jlleitschuh.gradle.ktlint") version "10.2.1" apply false
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

ext {
    version = '1.4.0'
    group = "com.ryftpay"
    java_version = JavaVersion.VERSION_1_8
    kotlin_jvm_target = '1.8'
    min_sdk_version = 21
    compile_sdk_version = 33
    target_sdk_version = 33
    build_tools_version = '31.0.0'
}

allprojects {
    repositories {
        // For local development
        mavenLocal()
        google()
        mavenCentral()
        maven {
            url 'https://maven.pkg.github.com/checkout/checkout-3ds-sdk-android'
            credentials {
                username = project.findProperty("ghUsername") ?: System.getenv('GH_USERNAME')
                password = project.findProperty("ghPackagesToken") ?: System.getenv('GH_PACKAGES_TOKEN')
            }
        }
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        reporters {
            reporter "html"
        }
    }
}

task clean (type: Delete) {
    delete rootProject.buildDir
}

apply from: "publish-root.gradle"